<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 width="450" height="220" fontFamily="宋体" fontSize="16">
	<fx:Script>
		<![CDATA[
			import mx.core.FlexGlobals;
			import mx.events.ResizeEvent;
			import mx.managers.PopUpManager;
			import mx.managers.SystemManager;
			
			import spark.components.Button;
			
			public static const YES:uint = 0x01;
			public static const NO:uint = 0x02;
			public static const OK:uint = 0x04;
			public static const CANCEL:uint = 0x08;
			
			private static var _yesLabel:String = "是";
			private static var _noLabel:String = "否";
			private static var _okLabel:String = "确定";
			private static var _cancelLabel:String = "取消";
			
			private var _closeHandler:Function;
			
			public static function show(text:String = "", title:String = "", flags:uint = 0x04, parent:DisplayObject = null, closeHandler:Function = null):void{
				if(parent == null){
					parent = DisplayObject(FlexGlobals.topLevelApplication);
				}
				var instance:Alert =  PopUpManager.createPopUp(parent, Alert, true) as Alert;
				PopUpManager.centerPopUp(instance);
				
				instance.contentTxt.text = text;
				instance.titleTxt.text = title;
				
				instance._closeHandler = closeHandler;
				
				instance.updateBtn(flags);
			}
			
			private function hide():void{
				removeBtn();
				
				PopUpManager.removePopUp(this);
			}
			
			private function creatBtn(btnID:String, btnLabel:String = "按钮"):void{
				var btn:spark.components.Button;
				btn = new spark.components.Button();
				btn.id = btnID;
				btn.label = btnLabel;
				btn.width = 100;
				btn.addEventListener(MouseEvent.CLICK, btnClickHandler);
				btnGroup.addElement(btn);
			}
			
			private function removeBtn():void{
				for each(var btn:spark.components.Button in btnGroup){
					if(btn){
						btn.removeEventListener(MouseEvent.CLICK, btnClickHandler);
					}
				}
				btnGroup.removeAllElements();
			}
			
			private function updateBtn(flags:uint):void{
				if(flags & 0x01){
					creatBtn("yesBtn", _yesLabel);
				}
				if(flags & 0x02){
					creatBtn("noBtn", _noLabel);
				}
				if(flags & 0x04){
					creatBtn("okBtn", _okLabel);
				}
				if(flags & 0x08){
					creatBtn("cancelBtn", _cancelLabel);
				}
			}
			
			private function btnClickHandler(event:MouseEvent):void{
				hide();
				if(_closeHandler != null){
					var detail:uint = 0x00;
					switch(event.currentTarget.id){
						case "yesBtn":
							detail = YES;
							break;
						case "noBtn":
							detail = NO;
							break;
						case "okBtn":
							detail = OK;
							break;
						case "cancelBtn":
							detail = CANCEL;
							break;
					}
					
					_closeHandler.call(null, detail);
				}
			}

			//------------------ getter/setter function -----------------------
			public static function get yesLabel():String
			{
				return _yesLabel;
			}

			public static function set yesLabel(value:String):void
			{
				_yesLabel = value;
			}

			public static function get noLabel():String
			{
				return _noLabel;
			}

			public static function set noLabel(value:String):void
			{
				_noLabel = value;
			}

			public static function get okLabel():String
			{
				return _okLabel;
			}

			public static function set okLabel(value:String):void
			{
				_okLabel = value;
			}

			public static function get cancelLabel():String
			{
				return _cancelLabel;
			}

			public static function set cancelLabel(value:String):void
			{
				_cancelLabel = value;
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<s:Panel width="450" height="220" alpha="0.8" backgroundColor="#000000" title="提示信息"
					   horizontalCenter="0" verticalCenter="0">
		<s:TextArea id="contentTxt" y="23" width="95%" height="107" borderVisible="false"
					color="#FFF600" contentBackgroundAlpha="0.0" editable="false" fontSize="20"
					fontWeight="normal" horizontalCenter="0" mouseChildren="false"
					mouseEnabled="false" paddingBottom="0" paddingLeft="0" paddingRight="0"
					paddingTop="0" selectable="false" text="内容"/>
		<s:Label id="titleTxt" x="8" y="12" width="555" color="#FEA629" fontSize="20"
				 fontWeight="bold" text="标题" visible="false"/>
		
	</s:Panel>
	<s:HGroup id="btnGroup" bottom="20" width="100" gap="10" horizontalCenter="0" paddingLeft="8"
			  paddingRight="8">
		<!--<s:Button id="yesBtn" label="是" click="btnClickHandler(event)" visible="true"/>
		<s:Button id="noBtn" label="否" click="btnClickHandler(event)" visible="true"/>
		<s:Button id="okBtn" label="确定" click="btnClickHandler(event)" visible="true"/>
		<s:Button id="cancelBtn" visible="true" label="取消" click="btnClickHandler(event)"/>-->
	</s:HGroup>
</s:Group>
